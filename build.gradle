buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'

private String parseModVersion() {
    def parsedModVersion = new groovy.json.JsonSlurper().parseText(file('mod_version.json').text)
    return parsedModVersion.major + '.' + parsedModVersion.semantic
}

def mod_version = parseModVersion()
println('Mod-Version: ' + mod_version)

version = "${mod_version}-${mc_version}-forge"
group = 'com.github.elenterius.biomancy'
archivesBaseName = 'biomancy'

// Mojang ships Java 17 to end users in 1.18+
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

sourceSets {
    api {}
    main {
        resources {
            srcDir 'src/generated/resources' // Include resources generated by data generators
        }
    }
    test
    datagen
}

minecraft {
    mappings channel: 'parchment', version: "${parchment_version}-${mc_version}"  // official mappings augmented with parameter names and javadocs

    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'mixin.debug.verbose', 'true'
            property 'mixin.debug.export', 'true'
            mods {
                biomancy {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        client2 {
            parent runs.client
            args '--username', 'Dev2'
        }

        // Hot-Swapping via JBR, includes schema changes
        // Guide: https://forge.gemwire.uk/wiki/Hotswap#Applying_schema_changes
        clientWithJBR {
            parent runs.client
            jvmArg '-XX:+AllowEnhancedClassRedefinition'
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'mixin.debug.verbose', 'true'
            property 'mixin.debug.export', 'true'
            mods {
                biomancy {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args '--mod', 'biomancy', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                biomancy {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.datagen
                }
            }
        }

        dataCI {
            parent runs.data
            forceExit false
        }
    }
}

//The mixin annotation processor doesn't have an obfuscation mapping when run through the IntelliJ compiler
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

mixin {
    // MixinGradle Settings
    add sourceSets.main, "mixins.biomancy.refmap.json"
    config 'mixins.biomancy.json'

    // options for dev run configs
    dumpTargetOnFailure = true
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }  // JEI
    maven { url = "https://modmaven.k-4u.nl" }  // JEI backup
    maven { url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url = 'https://maven.blamejared.com' }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://jitpack.io" }
}

configurations {
    datagenImplementation.extendsFrom minecraft
    apiImplementation.extendsFrom minecraft
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.19:3.1.39')

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-common-api:11.5.0.297")
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}-forge-api:11.5.0.297") // compile against the JEI API but do not include it at runtime
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}-forge:11.5.0.297") // at runtime, use the full JEI jar

//    compileOnly fg.deobf("curse.maven:jer_api-240630:3831562")
//    implementation "curse.maven:jer_deobf-240630:4092965"

    implementation fg.deobf("curse.maven:create-328085:4174330") //1.19.2-0.5.0.g
    runtimeOnly fg.deobf("curse.maven:flywheel-486392:4174759") //1.19.2-0.6.8

    runtimeOnly fg.deobf("curse.maven:farmers_delight-398521:3999157")
//    runtimeOnly fg.deobf("curse.maven:cultural_delights-574622:4000179") //download is forbidden >:(
//    runtimeOnly fg.deobf("com.ncpbails.culturaldelights:culturaldelights:1.18.2-0.14") //place jar inside libs folder in project root

    runtimeOnly fg.deobf("curse.maven:alexs_mobs-426558:4159154") //1.21.1
    runtimeOnly fg.deobf("curse.maven:citadel-331936:4132623")
    datagenImplementation fg.deobf("curse.maven:alexs_mobs-426558:4159154") //1.21.1

    compileOnly fg.deobf("com.github.Virtuoel:Pehkui:3.6.2-1.19.2-forge")

    //TODO: Morph Mod Integration?
//    implementation fg.deobf("curse.maven:identity-391390:3807264") // https://www.curseforge.com/minecraft/mc-mods/identity
//    runtimeOnly fg.deobf("curse.maven:architectury-419699:4040966")

    datagenImplementation sourceSets.main.output
    implementation sourceSets.api.output
}

import java.util.concurrent.TimeUnit

processResources {
    exclude '.cache'
    exclude '**/*.psd'
    exclude '**/*.bbmodel'
    doLast {
        minifyJsons(processResources.outputs.files.asPath)
    }
}

void minifyJsons(String fdir) {
    print "minmizing jsons..."
    long startTime = System.nanoTime()
    for (File file in fileTree(dir: fdir, include: "**/*.json")) {
        try {
            file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
        }
        catch (Exception e) {
            throw new Exception("Failed to minimize " + file, e)
        }
    }
    long ms = TimeUnit.MILLISECONDS.convert(System.nanoTime() - startTime, TimeUnit.NANOSECONDS)
    printf "%7s sec, %s ms \n", (int) (ms / 1000), ms % 1000
}

build {
    doLast {
    }
}

jar {
    // manifest properties for reading on runtime
    //noinspection GroovyAssignabilityCheck
    manifest {
        attributes([
                "Specification-Title"     : "biomancy",
                "Specification-Vendor"    : "Elenterius",
                "Specification-Version"   : "2",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : "Elenterius",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "mixins.biomancy.json"
        ])
    }
}

// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
